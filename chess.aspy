

new-game-board =
  [
  [ ♜ ♞ ♝ ♛ ♚ ♝ ♞ ♜ ]
  [ ♟ ♟ ♟ ♟ ♟ ♟ ♟ ♟ ]
  [ ＋ ＋ ＋ ＋ ＋ ＋ ＋ ＋ ]
  [ ＋ ＋ ＋ ＋ ＋ ＋ ＋ ＋ ]
  [ ＋ ＋ ＋ ＋ ＋ ＋ ＋ ＋ ]
  [ ＋ ＋ ＋ ＋ ＋ ＋ ＋ ＋ ]
  [ ♙ ♙ ♙ ♙ ♙ ♙ ♙ ♙ ]
  [ ♖ ♘ ♗ ♕ ♔ ♗ ♘ ♖ ]
  ]

piece at row :r col :c on :board = item c of ( item r of board )

rowstep S :r = r + 1
colstep S :c = c
row :r col :c is edge along S ? = r == 7

rowstep N :r = r - 1
colstep N :c = c
row :r col :c is edge along N ? = r == 0

rowstep E :r = r
colstep E :c = c + 1
row :r col :c is edge along E ? = c == 7

rowstep W :r = r
colstep W :c = c - 1
row :r col :c is edge along W ? = c == 0

rowstep NW :r = r - 1
colstep NW :c = c - 1
row :r col :c is edge along NW ? = ( c == 0 ) or ( r == 0 )

rowstep NE :r = r - 1
colstep NE :c = c + 1
row :r col :c is edge along NE ? = ( c == 7 ) or ( r == 0 )

rowstep SW :r = r + 1
colstep SW :c = c - 1
row :r col :c is edge along SW ? = ( c == 0 ) or ( r == 7 )

rowstep SE :r = r + 1
colstep SE :c = c + 1
row :r col :c is edge along SE ? = ( c == 7 ) or ( r == 7 )

piece along :dir from row :r col :c on :board =
  case
    row r col c is edge along dir ?
    edge
    piece at row ( rowstep dir r ) col ( colstep dir c ) on board == ＋
    piece along dir from row ( rowstep dir r ) col ( colstep dir c ) on board
    -true-
    piece at row ( rowstep dir r ) col ( colstep dir c ) on board


piece along S from row 3 col 3 on new-game-board
piece along N from row 3 col 3 on new-game-board
piece along E from row 3 col 3 on new-game-board
piece along W from row 3 col 3 on new-game-board
piece along NW from row 3 col 3 on new-game-board
piece along SW from row 3 col 3 on new-game-board
piece along NE from row 3 col 3 on new-game-board
piece along SE from row 3 col 3 on new-game-board

:board row :r col :c under attack ? =
  piece along S from row r col c on board == ♖
  or
  piece along N from row r col c on board == ♖
  or
  piece along E from row r col c on board == ♖
  or
  piece along W from row r col c on board == ♖

new-game-board row 6 col 0 under attack ?
